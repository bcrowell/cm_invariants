load(ctensor);
/* Schwarzschild spacetime */
ct_coords:[t,r,theta,phi];
lg:matrix([(1-2*m/r),0,0,0],
          [0,-1/(1-2*m/r),0,0],
          [0,0,-r^2,0],
          [0,0,0,-r^2*sin(theta)^2]);
load("cm.mac");

/* function to check for antisymmetry of a tensor on indices 1,2 and 3,4 */
check_symmetry(t,name):=block (
  print("checking symmetry of ",name),
  for i thru dim do
    for j thru dim do
      for k thru dim do
        for l thru dim do block (
          u:t[i,j,k,l],
          v:t[i,j,l,k],
          if u#(-v) then error("wrong symmetry on indices 3,4 at ",i,j,k,l," u,v=",u,",",v),
          v:t[j,i,k,l],
          if u#(-v) then error("wrong symmetry on indices 1,2 at ",i,j,k,l," u,v=",u,",",v)
      )
)
$

check_symmetry(cm_c,"C_ijkl");
check_symmetry(cm_c_mixed,"C_ij^kl");
check_symmetry(cm_c_star,"*C_ijkl");
check_symmetry(cm_c_star_upper,"*C^ijkl");
check_symmetry(cm_c_star_mixed,"*C_ij^kl");

/* function to check for tracelessness of a tensor on indices 1,3 and 2,3; it's assumed that one of these is upper and one lower */
check_traceless(t,name):=block (
  print("checking tracelessness of ",name),
  for i thru dim do
    for j thru dim do
      for k thru dim do block (
        tr13:sum(t[l,i,l,j],l,1,dim),
        if tr13#0 then error("not trace-free on indices 1,3 at ",i,j," trace=",tr13),
        tr23:sum(t[i,l,l,j],l,1,dim),
        if tr23#0 then error("not trace-free on indices 2,3 at ",i,j," trace=",tr23)
      )
)
$

check_traceless(cm_c_mixed,"C_ij^kl");
check_traceless(cm_c_star_mixed,"*C_ij^kl");
