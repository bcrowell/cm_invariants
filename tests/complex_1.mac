/* Test a simple metric for which some of the complex invariants have non-real values.
   Only one of these, as noted below, is known to be correct based on someone else's calculation. */

load(ctensor);
ct_coords:[t,x,y,z];
/* In the following, the factors of 2 just make the output a little simpler.
   The idea of using the exponentials is that they make the inverse metric have a simple form,
   so that expressions come out simple and don't take too long to compute. Cf. complex_2,
   in which the exponentials are not used. The only disadvantage of the exponentials is that
   they cause W1 to vanish, so we don't get a test of W1. */
lg:(1/2)*matrix([-exp(2*(x+y)),0,0,0],
          [0,exp(2*(z+t)),0,0],
          [0,0,1,0],
          [0,0,0,1])$
load("cm_invariants.mac");
cm_init();

a:exp(t+x+y+z);

cm_assert("complex_1 metric, Im(W2)",is(equal(imagpart(cm_w2()),12/a)));
cm_assert("complex_1 metric, Im(M1)",is(equal(imagpart(cm_m1()),4/a)));
cm_assert("complex_1 metric, Im(M2) at origin",is(equal(ev(imagpart(cm_m2()),t=0,x=0,y=0,z=0,eval),-1/6)));
     /* ... verified by Ron Lenk using Maple */
cm_assert("complex_1 metric, Im(M5) at origin",is(equal(ev(imagpart(cm_m5()),t=0,x=0,y=0,z=0,eval),-32/9)));



